<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.2" halcon_version="20.11.1.2">
<procedure name="main">
<interface/>
<body>
<c>*</c>
<c>* With this sample, you can perform one round of data acquisition and obtain the profile data (including intensity and depth data).</c>
<c>* In this sample, the data acquisition is triggered by the software, and the scanning of each line is triggered at a fixed rate.</c>
<c>*</c>
<c></c>
<c>* Discover all available laser profilers and store them in the "MechEyeProfilerInfos" variable.</c>
<c>* You can replace "LNX" with the "user_name" or "unique_name" of the laser profiler to be connected.</c>
<l>DeviceInfo := 'LNX'</l>
<l>info_framegrabber ('GigEVision2', 'device', Info, DeviceInfos)</l>
<l>tuple_regexp_select(DeviceInfos, DeviceInfo, MechEyeProfilerInfos)</l>
<l>dev_inspect_ctrl (MechEyeProfilerInfos)</l>
<c>* Stop execution if no laser profilers are found.</c>
<l>if (not |MechEyeProfilerInfos|)</l>
<l>    stop()</l>
<l>endif</l>
<c></c>
<c>* Connect to the laser profiler: if "user_name" or "unique_name" is not set in "DeviceInfo", the first laser profiler in "MechEyeProfilersInfos" will be connected. </c>
<l>open_framegrabber ('GigEVision2', 0, 0, 0, 0, 0, 0, 'default', -1, 'default', -1, 'false', 'default', MechEyeProfilerInfos[0], 0, -1, AcqHandle)</l>
<c></c>
<c>* Obtain the available parameter names and firmware version of the laser profiler.</c>
<l>get_framegrabber_param (AcqHandle, 'available_param_names', ParameterValues)</l>
<l>get_framegrabber_param (AcqHandle, 'DeviceFirmwareVersion', FirmwareVersion)</l>
<c>* Stop execution if the firmware version is lower than 2.2.0.</c>
<l>if (FirmwareVersion &lt; '2.2.0')</l>
<l>    stop()</l>
<l>endif</l>
<c>* Set the "grab_timeout" parameter.</c>
<c>* The "grab_timeout" parameter defines the maximum time in milliseconds that the frame grabber will wait for an image to be grabbed before timing out.</c>
<l>set_framegrabber_param (AcqHandle, 'grab_timeout', 20000)</l>
<c>* Set the "[Device]LinkCommandTimeout" parameter.</c>
<c>* The "[Device]LinkCommandTimeout" parameter defines the maximum time in microseconds that the frame grabber will wait for the reply of a control message before timing out.</c>
<c>* When increasing the number of lines to be scanned, please increase the "[Device]LinkCommandTimeout" parameter accordingly.</c>
<l>set_framegrabber_param (AcqHandle, '[Device]LinkCommandTimeout', 400000)</l>
<c>* Set the "[Stream]GevStreamMaxBlockDuration" parameter to 70% of the "grab_timeout" parameter.</c>
<c>* The "[Stream]GevStreamMaxBlockDuration" parameter defines the maximum transmission time for a data block in microseconds.</c>
<l>get_framegrabber_param (AcqHandle, 'grab_timeout', grabTimeout)</l>
<l>set_framegrabber_param (AcqHandle, '[Stream]GevStreamMaxBlockDuration', 0.7*(grabTimeout*1000))</l>
<c>* Set the maximum number of packet gaps allowed in the GigE Vision stream. </c>
<c>* A packet gap occurs when one or more packets are missing during the data transmission. </c>
<c>* Setting this parameter to 500 allows a maximum of 500 packet gaps in the stream.</c>
<l>set_framegrabber_param (AcqHandle, '[Stream]GevStreamMaxPacketGaps', 500)</l>
<c></c>
<c>* Set the "ExposureMode" parameter to "Timed" and the "ExposureTime" parameter to 80 Î¼s.</c>
<l>set_framegrabber_param (AcqHandle, 'ExposureMode', 'Timed')</l>
<l>set_framegrabber_param (AcqHandle, 'ExposureTime', 80)</l>
<c></c>
<c>* Set the "DeviceScanType" parameter to "Linescan3D" to obtain the profile data.</c>
<c>* If the laser profiler has not fully completed the data acquisition, the "DeviceScanType" parameter may be locked.</c>
<c>* Therefore, a for loop with wait is used to make up to 5 attempts at setting the parameter value.</c>
<l>for Index :=1 to 5 by 1 </l>
<l>    wait_seconds(2)</l>
<l>    try</l>
<l>        set_framegrabber_param (AcqHandle, 'DeviceScanType', 'Linescan3D')</l>
<l>        break</l>
<l>    catch (Exception)</l>
<l>        continue</l>
<l>    endtry </l>
<l>endfor</l>
<c>* Stop execution if all 5 attempts failed.</c>
<l>if (Index == 5)</l>
<l>    stop()</l>
<l>endif</l>
<c>* Set the "create_objectmodel3d" parameter to "enable" to create a 3D object model from the acquired profile data.</c>
<l>set_framegrabber_param (AcqHandle, 'create_objectmodel3d', 'enable')</l>
<c>* Set the "add_objectmodel3d_overlay_attrib" parameter to "enable" to add an overlay to the 3D object model using the intensity data.</c>
<l>set_framegrabber_param (AcqHandle, 'add_objectmodel3d_overlay_attrib', 'enable')</l>
<c>* Obtain the available options of the "ComponentSelector" parameter.</c>
<l>get_framegrabber_param (AcqHandle, 'ComponentSelector_values', componentSelectorValues)</l>
<c>* Set the "DataAcquisitionTriggerSource" parameter to "Software", so that data acquisition is triggered by the software.</c>
<c>* For Mech-Eye LNX-8080A: Comment out the following line.</c>
<l>set_framegrabber_param (AcqHandle, 'DataAcquisitionTriggerSource', 'Software')</l>
<c>* Set the "AcquisitionMode" to "SingleFrame", so that the laser profiler performs a single round of data acquisition.</c>
<l>set_framegrabber_param (AcqHandle, 'AcquisitionMode', 'SingleFrame')</l>
<c>* Set the "TriggerMode" parameter to "Off".</c>
<c>* The "TriggerMode" parameter controls if the selected trigger is active. </c>
<l>set_framegrabber_param (AcqHandle, 'TriggerMode', 'Off') </l>
<c>* Set the "LineScanTriggerSource" parameter to "FixedRate", so that the scanning of each line is triggered at a fixed rate.</c>
<c>* The fixed rate is set through the "AcquisitionLineRate" parameter.</c>
<l>set_framegrabber_param (AcqHandle, 'LineScanTriggerSource', 'FixedRate')</l>
<c>* Set the "AcquisitionLineRate" parameter to 2000 Hz.</c>
<l>set_framegrabber_param (AcqHandle, 'AcquisitionLineRate', 2000)</l>
<c>* Set the "Height" of image to 1600, which is equal to the number of lines to be scanned.</c>
<l>set_framegrabber_param (AcqHandle, 'Height', 1600 )</l>
<c>* Get the "Width" and "Height" of 3D object model.</c>
<l>get_framegrabber_param (AcqHandle, 'Width', Width)</l>
<l>get_framegrabber_param (AcqHandle, 'Height', Height)</l>
<c>* Set the "ComponentSelector" parameter to "Reflectance" and the "PixelFormat" parameter to "Mono8" to enable the acquisition of the intensity data. </c>
<l>set_framegrabber_param (AcqHandle, 'ComponentSelector', 'Reflectance')</l>
<l>set_framegrabber_param (AcqHandle, 'PixelFormat', 'Mono8')</l>
<c>* Set the "ComponentSelector" parameter to "Range" and the "PixelFormat" parameter to "Coord3D_C16" to enable the acquisition of the depth data.</c>
<l>set_framegrabber_param (AcqHandle, 'ComponentSelector', 'Range')</l>
<l>set_framegrabber_param (AcqHandle, 'PixelFormat', 'Coord3D_C16')</l>
<c>* Enable outlier removal by setting the `EnableOutlierRemoval` parameter to `true`.</c>
<c>* Adjust the outlier removal intensity by setting the `OutlierRemovalIntensity` parameter to `VeryLow`.</c>
<l>*if (FirmwareVersion &gt;= '2.5.0')</l>
<l>*    set_framegrabber_param (AcqHandle, 'EnableOutlierRemoval', true)</l>
<l>*    set_framegrabber_param (AcqHandle, 'OutlierRemovalIntensity', 'VeryLow')</l>
<l>*endif</l>
<c></c>
<c>* Acquire the 3D data from the laser profiler.</c>
<l>grab_data(Image, Region, Contours, AcqHandle, ObjectModel3D)</l>
<c>* Select the "Reflectance" component (intensity data) from the acquired data and store it to the variable "reflectance". </c>
<l>select_obj (Image, reflectance, 1)</l>
<c>* Get the width and height of the intensity image and store the values in the variables "reflectanceWidth" and "reflectanceHeight".</c>
<l>get_image_size (reflectance, reflectanceWidth, reflectanceHeight)</l>
<c>* Select the "Range" component (depth data) from the acquired data and store it to the variable "depthRaw".</c>
<l>select_obj(Image, depthRaw, 2)</l>
<c>* Set the "ChunkComponentSelector" parameter to "Range" to get the scaling factor and offset for the Z (depth) values. </c>
<l>set_framegrabber_param (AcqHandle, 'ChunkComponentSelector', 'Range')</l>
<l>get_framegrabber_param (AcqHandle, 'ChunkScan3dCoordinateScale', scaleZ) </l>
<l>get_framegrabber_param (AcqHandle, 'ChunkScan3dCoordinateOffset', offsetZ)</l>
<c>* Transform the Z values using the scaling factor and offset.</c>
<l>convert_image_type(depthRaw, depthReal, 'real')</l>
<c>* Filter out the points with invalid depth values.</c>
<l>threshold(depthReal, validRegion, 1e-30, 65535)</l>
<l>reduce_domain(depthReal, validRegion, depthValid) </l>
<c>* Scale the depth map using the "scaleZ" and "offsetZ" values, and store the scaled result to the variable "depthInMM".</c>
<l>scale_image(depthValid, depthInMM, scaleZ, offsetZ)</l>
<c></c>
<l>if (FirmwareVersion &gt; '2.3.1')</l>
<c>    * Obtain the number of points in the point cloud.</c>
<l>    get_object_model_3d_params(ObjectModel3D, 'num_points', NumOfPoints)</l>
<c>    * Check if the number of points is zero. If not, save the point cloud to the "PointCloud" PLY file.</c>
<l>    if(NumOfPoints != 0)</l>
<l>        write_object_model_3d (ObjectModel3D, 'ply', 'PointCloud.ply', [], [])</l>
<c>        * Uncomment this function if you would like to retrieve the point cloud in the custom reference frame.</c>
<c>        * The custom reference frame can be adjusted using the "Custom Reference Frame" tool in Mech-Eye</c>
<c>        * Viewer. The rigid body transformations are automatically calculated after the settings in this</c>
<c>        * tool have been applied. The "Custom Reference Frame" tool is recommended as the GUI allows you to</c>
<c>        * adjust the reference frame easily and conveniently.</c>
<l>        *if (FirmwareVersion &gt;= '2.4.0')</l>
<l>        *    transformAndSavePointCloud(AcqHandle,ObjectModel3D) </l>
<l>        *endif  </l>
<l>    endif</l>
<c>    </c>
<c>    * Visualize the point cloud.</c>
<c>    * Note: If the value of the "Height" parameter is greater than 32768, the visualization window cannot open, and the error with code 5122 will be raised.</c>
<c>    * In that case, please deactivate the next three lines.</c>
<l>    dev_open_window (0, 0, Width, Height, 'black', WindowHandle3d)</l>
<l>    visualize_object_model_3d (WindowHandle3d, ObjectModel3D, [], [], [], [], [], [], [], PoseOut)</l>
<l>    dev_close_window ()</l>
<l>endif</l>
<c></c>
<c>* Clear the 3D object model stored in the variable "ObjectModel3D", preparing it for further processing or storage of new data. </c>
<l>clear_object_model_3d (ObjectModel3D)</l>
<c></c>
<c>* Close the frame grabber.</c>
<l>close_framegrabber (AcqHandle)</l>
<c></c>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
<procedure name="transformAndSavePointCloud">
<interface>
<ic>
<par name="AcqHandle" base_type="ctrl" dimension="0"/>
<par name="ObjectModel3D" base_type="ctrl" dimension="0"/>
</ic>
</interface>
<body>
<c>* Define a default transformation matrix.</c>
<l>hom_mat3d_identity( HomMat3D )</l>
<c>* Obtain the Euler angles and translation vector.</c>
<l>set_framegrabber_param (AcqHandle, 'Scan3dCoordinateTransformSelector', 'RotationX')</l>
<l>get_framegrabber_param (AcqHandle, 'Scan3dTransformValue',AngleX)</l>
<l>set_framegrabber_param (AcqHandle, 'Scan3dCoordinateTransformSelector', 'RotationY')</l>
<l>get_framegrabber_param (AcqHandle, 'Scan3dTransformValue',AngleY)</l>
<l>set_framegrabber_param (AcqHandle, 'Scan3dCoordinateTransformSelector', 'RotationZ')</l>
<l>get_framegrabber_param (AcqHandle, 'Scan3dTransformValue',AngleZ)</l>
<l>set_framegrabber_param (AcqHandle, 'Scan3dCoordinateTransformSelector', 'TranslationX')</l>
<l>get_framegrabber_param (AcqHandle, 'Scan3dTransformValue', Tx)</l>
<l>set_framegrabber_param (AcqHandle, 'Scan3dCoordinateTransformSelector', 'TranslationY')</l>
<l>get_framegrabber_param (AcqHandle, 'Scan3dTransformValue', Ty)</l>
<l>set_framegrabber_param (AcqHandle, 'Scan3dCoordinateTransformSelector', 'TranslationZ')</l>
<l>get_framegrabber_param (AcqHandle, 'Scan3dTransformValue', Tz)</l>
<c>* Convert the Euler angles to radians.</c>
<l>AngleX := AngleX * (3.141592653589793 / 180)</l>
<l>AngleY := AngleY * (3.141592653589793 / 180)</l>
<l>AngleZ := AngleZ * (3.141592653589793 / 180)</l>
<c>* Add the rotations to the HomMat3D matrix.</c>
<l>hom_mat3d_rotate(HomMat3D, AngleX,'x',0, 0, 0, HomMat3D) </l>
<l>hom_mat3d_rotate(HomMat3D, AngleY,'y',0, 0, 0, HomMat3D)</l>
<l>hom_mat3d_rotate(HomMat3D, AngleZ,'z',0, 0, 0, HomMat3D)  </l>
<c>* Add the translations to the HomMat3D matrix.</c>
<l>hom_mat3d_translate( HomMat3D, Tx, Ty, Tz, HomMat3D)</l>
<c>* Use the HomMat3D matrix to transform the 3D object model.</c>
<l>affine_trans_object_model_3d(ObjectModel3D,HomMat3D,TransformedPointCloud)</l>
<c>* Save the transformed point cloud to the "TransformedPointCloud" PLY file.</c>
<l>write_object_model_3d (TransformedPointCloud, 'ply', 'TransformedPointCloud.ply', [], [])</l>
</body>
<docu id="transformAndSavePointCloud">
<parameters>
<parameter id="AcqHandle"/>
<parameter id="ObjectModel3D"/>
</parameters>
</docu>
</procedure>
</hdevelop>
